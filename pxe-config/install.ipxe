#!ipxe

set menu-timeout 300000
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default massdriver
set server_ip http://192.168.1.250/tftpboot
set disks ${server_ip}/samba/install/disks
set scripts ${server_ip}/samba/install/scripts/internal
:start
menu
item --gap --           -------------WINPE----------------
item winpe           	winpe	
item massdriver		winpe avec mass de driver
item acer		winpe pour les acer
item install_acer	install ++ acer
item tmp		tmp
item --gap --           -------------INSTALL WINDOWS AUTO----------------
item famille		auto install PC Famille
item famille-bios	auto install PC famille en BIOS
item famille-uefi	auto install PC famille en UEFI
item install-withapp	auto install window avec MAJ et APPS
item --gap --           -------------LINUX----------------
item mint      		linux mint
item kali		kali linux
item --gap --           -------------UTILS----------------
item pxelinux		pxelinux
item shell              Shell iPXE
item reboot		reboot
item shutdown		stop
item exit              	Exit

choose --timeout ${menu-timeout} --default ${menu-default} target && goto ${target}

:winpe
kernel ${server_ip}/wimboot
initrd ${disks}/winPE/winpefr/Boot/BCD		BCD
initrd ${disks}/winPE/winpefr/Boot/boot.sdi	boot.sdi
initrd ${disks}/winPE/winpefr/sources/boot.wim	boot.wim
boot

:massdriver
kernel ${server_ip}/wimboot
initrd ${disks}/winPE/winpe_massdriver/Boot/BCD		BCD
initrd ${disks}/winPE/winpe_massdriver/Boot/boot.sdi	boot.sdi
initrd ${disks}/winPE/winpe_massdriver/sources/boot.wim	boot.wim
boot

:acer
kernel ${server_ip}/wimboot
initrd ${disks}/winPE/winpe_acer/Boot/BCD		BCD
initrd ${disks}/winPE/winpe_acer/Boot/boot.sdi		boot.sdi
initrd ${disks}/winPE/winpe_acer/sources/boot.wim	boot.wim
boot

:install_acer
initrd ${scripts}/install_withapp.bat		install.bat
initrd ${scripts}/winpeshl.ini   		winpeshl.ini
goto acer

:tmp
chain boot/pxeboot.n12 || goto failed
## chain tftp://192.168.1.250/boot/pxeboot.n12

:mint
kernel ${disks}/linux/mint-21.2/casper/vmlinuz
initrd ${disks}/linux/mint-21.2/casper/initrd.lz
imgargs vmlinuz initrd=initrd root=/dev/nfs boot=casper netboot=nfs nfsroot=192.168.1.250:/srv/tftpboot/samba/install/disks/linux/mint-21.2 ip=dhcp --
boot

:kali
kernel ${server_ip}/kali/live/vmlinuz
initrd ${server_ip}/kali/live/initrd.img
imgargs vmlinuz initrd=initrd root=/dev/nfs boot=live netboot=nfs nfsroot=192.168.1.250:/srv/tftpboot/samba/install/disks/linux/kali ip=dhcp --
boot

:install-withapp
initrd ${scripts}/install_withapp.bat		install.bat
initrd ${scripts}/winpeshl.ini   		winpeshl.ini
goto massdriver

:famille
initrd ${scripts}/install_famille.bat		install.bat
initrd ${scripts}/winpeshl.ini			winpeshl.ini
goto massdriver

:famille-uefi
initrd ${scripts}/install_famille_uefi.bat	install.bat
initrd ${scripts}/winpeshl.ini			winpeshl.ini
goto massdriver

:famille-bios
initrd ${scripts}/install_famille_bios.bat	install.bat
initrd ${scripts}/winpeshl.ini			winpeshl.ini
goto massdriver

:pxelinux
dhcp net0
set 210:string tftp://192.168.1.250/
#set 210:string tftp://${dhcp-server}/
chain ${210:string}pxelinux.0 || goto failed
goto start


:reboot
reboot

:shutdown
poweroff

:shell
shell

:exit
exit
